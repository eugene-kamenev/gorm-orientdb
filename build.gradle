buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1"
        classpath "org.grails:grails-gradle-plugin:3.1.8"
        classpath "org.grails:grails-gradle-plugin:2.2.0.RC1"
    }
}

group "org.grails"
version project.gormVersion
println "GORM VERSION = ${project.gormVersion}"

ext {
    grailsVersion = "3.0.12"
    groovyVersion = "2.4.7"
    springBootVersion = "1.3.5.RELEASE"
    orientDbVersion = '2.2.3'
    springVersion = "4.1.7.RELEASE"
    servletApiVersion = "3.0.1"
}


subprojects { project ->
    boolean isPluginProject = project.name.endsWith("-plugin") && (project.name.startsWith("grails") || project.name.startsWith("rx-"))
    boolean isGrails3PluginProject = project.name.endsWith("-plugin") && !project.name.startsWith("grails2")
    boolean isGrails2PluginProject = project.name.endsWith("-plugin") && project.name.startsWith("grails2")

    ext {
        projectInfo = new PublishingConvention(project)
    }


    if(isPluginProject) {
        group "org.grails.plugins"
    }
    else {
        group "org.grails"
    }

    version project.gormVersion

    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }

    configurations {
        documentation

        all {
            resolutionStrategy {
                cacheDynamicVersionsFor 0, 'hours'
                cacheChangingModulesFor 0, 'hours'
            }
        }
    }

    if(isPluginProject) {
        group "org.grails.plugins"
    }
    else {
        group "org.grails"
    }

    if(isGrails3PluginProject) {
        apply plugin: "org.grails.grails-plugin"
    }
    else if(!isGrails2PluginProject) {
        apply plugin:"groovy"
    }

    if(!isGrails2PluginProject) {
        apply plugin: "io.github.groovylang.groovydoc"


        sourceCompatibility = "1.7"
        targetCompatibility = "1.7"

        dependencies {
            documentation "org.fusesource.jansi:jansi:1.11"
            testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
        }

        groovydoc.jvmArgs "-Xmx512M"
        groovydoc.classpath += configurations.documentation

    }
}

class PublishingConvention {
    Project project

    String projectArtifactId
    String projectName = 'GORM for OrientDB'
    String projectDescription = 'Provides a GORM Object Mapping implementations for the OrientDB Document Database'
    String projectURL = 'https://gorm.grails.org/latest/mongodb'
    String projectIssueTrackerUrl = 'https://github.com/grails/gorm-mongodb/issues'
    String projectVcsUrl = 'https://github.com/grails/gorm-mongodb'

    PublishingConvention(Project project) {
        this.project = project

        def name = project.name
        if(name.startsWith('grails') && name.endsWith('-plugin')) {
            name = 'orientdb'
        }
        projectArtifactId = name
    }
}